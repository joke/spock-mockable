plugins {
    id 'java'
    id 'groovy'
}

def spock13 = ['spock13groovy24', 'spock13groovy25']
def spock2x = ['spock20groovy25', 'spock20groovy30', 'spock21groovy25', 'spock21groovy30', 'spock22groovy25', 'spock22groovy30', 'spock22groovy40']

[*spock13, *spock2x].forEach {
    sourceSets.create(it) {
        java.srcDirs += sourceSets.main.java.srcDirs
        groovy.srcDirs += sourceSets.test.groovy.srcDirs
    }
}

dependencies {
    testImplementation platform('org.codehaus.groovy:groovy-bom:3.0.9')
    testImplementation platform('org.spockframework:spock-bom:2.1-groovy-3.0')
    testImplementation project(':spock-mockable')
    testImplementation 'org.slf4j:slf4j-simple:2.0.0'
    testImplementation 'org.spockframework:spock-core'

    spock13groovy24Implementation platform('org.codehaus.groovy:groovy-bom:2.4.21')
    spock13groovy24Implementation platform('org.spockframework:spock-bom:1.3-groovy-2.4')
    spock13groovy24Implementation project(':spock-mockable')
    spock13groovy24Implementation 'org.slf4j:slf4j-simple:1.7.36'
    spock13groovy24Implementation 'org.spockframework:spock-core'
    spock13groovy24Implementation 'org.objenesis:objenesis:3.3'

    spock13groovy25Implementation platform('org.codehaus.groovy:groovy-bom:2.5.16')
    spock13groovy25Implementation platform('org.spockframework:spock-bom:1.3-groovy-2.5')
    spock13groovy25Implementation project(':spock-mockable')
    spock13groovy25Implementation 'org.slf4j:slf4j-simple:1.7.36'
    spock13groovy25Implementation 'org.spockframework:spock-core'
    spock13groovy25Implementation 'org.objenesis:objenesis:2.6'

    spock20groovy25Implementation platform('org.codehaus.groovy:groovy-bom:2.5.16')
    spock20groovy25Implementation platform('org.spockframework:spock-bom:2.0-groovy-2.5')
    spock20groovy25Implementation project(':spock-mockable')
    spock20groovy25Implementation 'org.slf4j:slf4j-simple:1.7.36'
    spock20groovy25Implementation 'org.spockframework:spock-core'

    spock20groovy30Implementation platform('org.codehaus.groovy:groovy-bom:3.0.9')
    spock20groovy30Implementation platform('org.spockframework:spock-bom:2.0-groovy-3.0')
    spock20groovy30Implementation project(':spock-mockable')
    spock20groovy30Implementation 'org.slf4j:slf4j-simple:1.7.36'
    spock20groovy30Implementation 'org.spockframework:spock-core'

    spock21groovy25Implementation platform('org.codehaus.groovy:groovy-bom:2.5.16')
    spock21groovy25Implementation platform('org.spockframework:spock-bom:2.1-groovy-2.5')
    spock21groovy25Implementation project(':spock-mockable')
    spock21groovy25Implementation 'org.slf4j:slf4j-simple:1.7.36'
    spock21groovy25Implementation 'org.spockframework:spock-core'

    spock21groovy30Implementation platform('org.codehaus.groovy:groovy-bom:3.0.9')
    spock21groovy30Implementation platform('org.spockframework:spock-bom:2.1-groovy-3.0')
    spock21groovy30Implementation project(':spock-mockable')
    spock21groovy30Implementation 'org.slf4j:slf4j-simple:1.7.36'
    spock21groovy30Implementation 'org.spockframework:spock-core'

    spock22groovy25Implementation platform('org.codehaus.groovy:groovy-bom:2.5.16')
    spock22groovy25Implementation platform('org.spockframework:spock-bom:2.2-M1-groovy-2.5')
    spock22groovy25Implementation project(':spock-mockable')
    spock22groovy25Implementation 'org.slf4j:slf4j-simple:1.7.36'
    spock22groovy25Implementation 'org.spockframework:spock-core'

    spock22groovy30Implementation platform('org.codehaus.groovy:groovy-bom:3.0.9')
    spock22groovy30Implementation platform('org.spockframework:spock-bom:2.2-M1-groovy-3.0')
    spock22groovy30Implementation project(':spock-mockable')
    spock22groovy30Implementation 'org.slf4j:slf4j-simple:1.7.36'
    spock22groovy30Implementation 'org.spockframework:spock-core'

    spock22groovy40Implementation platform('org.apache.groovy:groovy-bom:4.0.5')
    spock22groovy40Implementation platform('org.spockframework:spock-bom:2.2-M1-groovy-4.0')
    spock22groovy40Implementation project(':spock-mockable')
    spock22groovy40Implementation 'org.slf4j:slf4j-simple:1.7.36'
    spock22groovy40Implementation 'org.spockframework:spock-core'
}

spock13.forEach { source ->
    tasks.register("test$source", Test) {
        group = 'verification'

        testClassesDirs = sourceSets.findByName(source).output.classesDirs
        classpath = sourceSets.findByName(source).runtimeClasspath

        useJUnit()
        tasks.findByName('check').dependsOn it
    }
}
spock2x.forEach { source ->
    tasks.register("test$source", Test) {
        group = 'verification'

        testClassesDirs = sourceSets.findByName(source).output.classesDirs
        classpath = sourceSets.findByName(source).runtimeClasspath

        useJUnitPlatform()
        tasks.findByName('check').dependsOn it
    }
}

test {
    enabled = false
}

tasks.withType(Test) {
    forkEvery 1
}
