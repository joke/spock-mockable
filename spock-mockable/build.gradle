plugins {
    id 'java-library'
    id 'jacoco'
    id 'groovy'
    id 'io.freefair.lombok'
    id 'net.ltgt.errorprone'
    id 'com.github.johnrengelman.shadow'
    id 'maven-publish'
}

dependencies {
    errorprone enforcedPlatform(project(':dependencies'))
    errorprone 'com.google.errorprone:error_prone_core'
    errorprone 'com.uber.nullaway:nullaway'

    annotationProcessor enforcedPlatform(project(':dependencies'))
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.jetbrains:annotations'
    annotationProcessor 'com.google.auto.service:auto-service'
    annotationProcessor 'com.google.dagger:dagger-compiler'

    compileOnly enforcedPlatform(project(':dependencies'))
    compileOnly 'com.google.auto.service:auto-service-annotations'
    compileOnly 'com.google.dagger:dagger'
    compileOnly 'org.apache.groovy:groovy'
    compileOnly 'org.jetbrains:annotations'
    compileOnly 'org.junit.platform:junit-platform-engine'
    compileOnly 'org.junit.platform:junit-platform-launcher'
    compileOnly 'org.projectlombok:lombok'
    compileOnly 'org.spockframework:spock-core'

    api 'com.google.dagger:dagger:2.44.2'

    shadow 'net.bytebuddy:byte-buddy-agent:1.12.18'
    shadow 'net.bytebuddy:byte-buddy:1.12.18'
    shadow 'org.slf4j:slf4j-api:2.0.3'

    testImplementation enforcedPlatform(project(':dependencies'))
    testImplementation 'io.github.joke:spock-deepmock'
    testImplementation 'io.github.joke:spock-outputcapture'
    testImplementation 'net.bytebuddy:byte-buddy'
    testImplementation 'net.bytebuddy:byte-buddy-agent'
    testImplementation 'org.objenesis:objenesis'
    testImplementation 'org.slf4j:slf4j-simple'
    testImplementation 'org.spockframework:spock-core'
}

test {
    useJUnitPlatform()
    jvmArgs = [
            '-Dorg.slf4j.simpleLogger.defaultLogLevel=TRACE',
            '-Dspock-mockable.disabled=true'
    ]
}

jar {
    classifier 'plain'
}

shadowJar {
    relocate 'dagger', 'io.github.joke.spockmockable.shadow.dagger'
    relocate 'javax', 'io.github.joke.spockmockable.shadow.javax'
    classifier null
    minimize()
}

jar {
    manifest {
        attributes(
                'Can-Redefine-Classes': 'true',
                'Can-Retransform-Classes': 'true',
                'Premain-Class': 'io.github.joke.spockmockable.agent.InstrumentationInstaller',
        )
    }
}

publishing {
    publications {
        spockMockable(MavenPublication) { publication ->
            project.shadow.component publication
            artifact sourcesJar
            artifact javadocJar
        }
    }
}
